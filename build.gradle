buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE'
        classpath 'org.yaml:snakeyaml:1.15'
        classpath 'com.moowork.gradle:gradle-node-plugin:0.9'
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.2'
    }
}

// Constants
def javaVersion = "1.8"
def springVersion = "4.2.2.RELEASE"
def springBootVersion = "1.3.1.RELEASE"
def queryDSLVersion = "3.7.0"

allprojects {

    apply plugin: 'java'
    apply plugin: 'eclipse'
    group = 'com.curriculum'
    version = '0.0.1'
}

subprojects {

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {

        //Spring
        compile "org.springframework.boot:spring-boot:${springBootVersion}"
        compile "org.springframework.boot:spring-boot-actuator:${springBootVersion}"
        compile "org.springframework.boot:spring-boot-autoconfigure:${springBootVersion}"
        compile "org.springframework.boot:spring-boot-loader-tools:${springBootVersion}"
        compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
        compile "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
        compile "org.springframework.boot:spring-boot-starter-data-mongodb:${springBootVersion}"
        compile "org.springframework.boot:spring-boot-starter-aop:${springBootVersion}"
        compile "org.springframework.boot:spring-boot-starter-logging:${springBootVersion}"

        //Testing
        testCompile 'junit:junit:4.12'
        testCompile 'org.hamcrest:hamcrest-all:1.3'
        testCompile 'org.mockito:mockito-core:1.10.19'
        testCompile "org.springframework:spring-test:${springVersion}"

        //Other
        compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.7.3'
        compile 'joda-time:joda-time:1.3'
        compile 'org.apache.commons:commons-lang3:3.4'
        compile 'org.apache.commons:commons-io:1.3.2'
        compile 'org.apache.xmlgraphics:fop:0.93'
    }
}

project(':dto') {}

project(':domain') {
    configurations { querydslapt }

    sourceSets {
        generated {
            java {
                srcDir 'src/main/generated'
            }
        }
    }

    dependencies {

        // QueryDSL
        compile "com.mysema.querydsl:querydsl-core:${queryDSLVersion}"
        compile "com.mysema.querydsl:querydsl-mongodb:${queryDSLVersion}"
        querydslapt "com.mysema.querydsl:querydsl-apt:${queryDSLVersion}"
    }

    task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
        println 'generateQueryDSL'
        source = sourceSets.main.java
        classpath = configurations.compile + configurations.querydslapt
        options.compilerArgs = [
                "-proc:only",
                "-processor",
                "org.springframework.data.mongodb.repository.support.MongoAnnotationProcessor"
        ]
        destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
    }

    compileJava {
        println 'compileJava'
        dependsOn generateQueryDSL
        source generateQueryDSL.destinationDir
    }

    compileGeneratedJava {
        println 'compileGeneratedJava'
        dependsOn generateQueryDSL
        options.warnings = true
        classpath += sourceSets.main.runtimeClasspath
    }

    clean {
        delete sourceSets.generated.java.srcDirs
    }

}

project(':persistence') {

    dependencies {
        compile project(':domain')
    }

}

project(':service') {

    dependencies {
        compile project(':domain')
        compile project(':persistence')
    }
}

project(':converter') {
    dependencies {
        compile project(':domain')
        compile project(':dto')
    }
}

project(':facade') {

    dependencies {
        compile project(':domain')
        compile project(':service')
        compile project(':converter')
    }
}

project(':controller') {

    apply plugin: 'spring-boot'

    dependencies {
        compile project(':dto')
        compile project(':domain')
        compile project(':facade')
        compile project(':service')
        compile project(':persistence')
        compile "org.springframework.boot:spring-boot-starter-tomcat:${springBootVersion}"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

task buildFrontend(type: Exec) {
    workingDir "$projectDir/ui"
    commandLine 'grunt', 'build'
}

task copyFrontend(type: Exec, dependsOn: 'buildFrontend') {
    workingDir "$projectDir"
    commandLine 'rsync', '-r', 'ui/dist/', 'controller/src/main/resources/static'
}

task buildBackend(type: Exec, dependsOn: 'copyFrontend') {
    workingDir "$projectDir"
    commandLine 'gradle', 'clean', 'build'
}

task deploy(dependsOn: 'buildBackend') {
}